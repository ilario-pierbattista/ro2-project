% !TEX root=index.tex
\chapter{Implementazione del Modello in AMPL}\label{ch:ampl}
	Nel seguente capitolo, dopo una breve presentazione del linguaggio di modellazione, verrà trattata l’implementazione sviluppata del \emph{Pollution Routing Problem} su calcolatore.

	\section{Il Linguaggio di Modellazione AMPL} % (fold)
	\label{sec:il_linguaggio_di_modellazione_ampl}
		AMPL (\emph{A Mathematical Programming Language}) è un linguaggio di modellazione\footnote{Un linguaggio di modellizzazione (modelling language in inglese) è un linguaggio formale che può essere utilizzato per descrivere (modellizzare) un sistema di qualche natura} algebrica	per descrivere e risolvere problemi di alta complessità che necessitano di calcoli matematici su larga scala (es.: problemi di  scheduling). 
		
		% Aggiungere riferimenti al manuale utente di AMPL
		È stato sviluppato a metà degli anni ‘80 da Robert Fourer, David Gay e Brian Kernighan presso i Laboratori Bell, gli stessi laboratori che sfornarono idee ed invenzioni rivoluzionarie come il transistor e il sistema operativo UNIX.

		Il punto forte di AMPL rispetto ad altri linguaggi di modellazione sta nella estrema somiglianza della sintassi alla notazione matematica dell’ottimizzazione di problemi. 
		Questo fa sì che i problemi possano essere espressi in una forma estremamente compatta e leggibile (i listati \ref{code:lingo_example} e \ref{code:ampl_example} mettono a confronto la modellazione del classico problema dei trasporti in LINGO e in AMPL).

		\begin{minted}[tabsize=4,obeytabs]{lingo}
			MODEL:
			! A 6 Warehouse 8 Vendor Transportation Problem;
			SETS:
			  WAREHOUSES / WH1 WH2 WH3 WH4 WH5 WH6/: CAPACITY;
			  VENDORS / V1 V2 V3 V4 V5 V6 V7 V8/   : DEMAND;
			  LINKS( WAREHOUSES, VENDORS): COST, VOLUME;
			ENDSETS
			! The objective;
			  MIN = @SUM( LINKS( I, J):
			   COST( I, J) * VOLUME( I, J));
			! The demand constraints;
			  @FOR( VENDORS( J):
			   @SUM( WAREHOUSES( I): VOLUME( I, J)) =
			    DEMAND( J));
			! The capacity constraints;
			  @FOR( WAREHOUSES( I):
			   @SUM( VENDORS( J): VOLUME( I, J)) <=
			    CAPACITY( I));
			! [...];
			END
		\end{minted}
		\captionof{listing}{Il problema dei traporti in LINGO \label{code:lingo_example}}

		\begin{amplcode}
			set ORIG;						# origins
			set DEST;						# destinations

			param supply {ORIG} >= 0;		# amounts available at origins
			param demand {DEST} >= 0;		# amounts required at destinations

			check: sum {i in ORIG} supply[i] = sum {j in DEST} demand[j];
			
			param cost {ORIG,DEST} >= 0;	# shipment costs per unit
			
			var Trans {ORIG,DEST} >= 0;		# units to be shipped

			minimize Total_Cost:sum {i in ORIG, j in DEST} cost[i,j] * Trans[i,j];
			subject to Supply {i in ORIG}:sum {j in DEST} Trans[i,j] = supply[i];
			subject to Demand {j in DEST}:sum {i in ORIG} Trans[i,j] = demand[j];

		\end{amplcode}
		\captionof{listing}{Il problema dei traporti in AMPL \label{code:ampl_example}}

		Un altro vantaggio considerevole dell’AMPL è il supporto a decine di risolutori, sia open source che a pagamento, tra cui i più i famosi CBC, CPLEX e MINOS.
		Ad oggi, secondo le statistiche fornite dal NEOS (web server risolutore della University of Wisconsin in Madison), AMPL risulta essere il linguaggio di modellazione matematica più utilizzato.
	% section il_linguaggio_di_modellazione_ampl (end)

	\section{Implementazione} % (fold)
	\label{sec:implementazione}
		In AMPL un problema di programmazione lineare viene modellato indipendentemente dalla singola istanza del problema. In un file, con estensione “mod”, viene scritto, utilizzando un linguaggio puramente dichiarativo, il modello matematico lasciandone liberi i singoli parametri. In un altro file, con estensione “dat”, vengono impostati i valori dei parametri dichiarati nel modello.
		Questa architettura modulare consente di disaccoppiare con estrema semplicità la formulazione del modello e l’immissione dei dati.

		I comandi da immettere nella console di AMPL all’atto esecuzione saranno semplicemente:	
		\begin{amplcode}
			ampl: model 'file_del_modello.mod';
			ampl: data 'file_dei_dati.dat';
			ampl: solve;
		\end{amplcode}

		Il risolutore di default è MINOS che non supporta problemi di PLI o di PLM. Sarà necessario specificare manualmente il risolutore CPLEX, prima di eseguire il comando \texttt{solve}:

		\begin{amplcode}
			ampl: option solver cplex;
		\end{amplcode}

		La struttura del file contenente il modello è molto semplice. 
		Si comincia con la dichiarazione degli insiemi (\texttt{set}) e dei parametri (\texttt{param}). Quindi si andranno a definire le variabili decisionali (\texttt{var}) e la funzione obiettivo. Alla fine sarà presente l’elenco dei vincoli ai quali le variabili decisionali sono soggette.

		Per dichiarare un insieme, struttura dati equivalente al corrispondente algebrico,  è sufficiente far seguire alla parola chiave \texttt{set} l’identificatore scelto per l’insieme stesso. Sono supportate tutte le operazioni insiemistiche già in fase di dichiarazione: infatti è possibile dichiarare l’insieme \texttt{path} come prodotto cartesiano di \texttt{cities} ancor prima di aver definito il reale contenuto dell’insieme \texttt{cities}.

		\begin{amplcode}
			set cities;				# Insieme dei nodi nella rete stradale
			set depot within cities;# Nodo deposito
			set clients within cities := cities diff depot;	
									# Insieme dei nodi clienti
			set speed_levels;		# Insieme dei livelli assumibili di velocita
			set path := cities cross cities;	
									# Insieme degli archi			
		\end{amplcode}

		In seguito vengono dichiarati i parametri, che unitamente agli insiemi identificano la specifica istanza del problema di ottimizzazione. Sono molto numerosi e vengono raggruppati in base al contesto di competenza.

		\begin{amplcode}
			### PARAMETRI GENERALI ###

			param ga > 0;			# Costante d'accelerazione gravitazionale
			param ad > 0; 			# Densita dell'aria in kg/m3
			param fc > 0;			# Costo del carburante in euro/litro
			param ec > 0;			# Costo delle emissioni in euro/Kg
			param ghgfe > 0;		# Quantita di emissioni di gas GHG per litro
									# di carburante (in Kg/litro)
			param diesel_energy > 0;# Quantita di energia intrinseca in un litro
									# di carburante (in J)
			param engine_efficiency > 0;
									# Efficienza media del motore del veicolo
			param L >= 0;			# Un numero "sufficientemente grande"


			### PARAMETRI RELATIVI AI VEICOLI ###

			param fleet_dim > 0 integer;	
									# Numero di veicoli appartenenti alla flotta
			param gvw 'gross vehicle weight' > 0;				
									# Massa a pieno carico in kg
			param uvw 'unladen vehicle weight' > 0;
									# Massa a vuoto in kg
			param max_load := gvw - uvw;	# Carico massimo dei veicoli in kg
			param rrc 'rolling resistence coefficient' > 0;
									# Coefficiente di resistenza al rotolamento
			param drc 'drag resistence coefficient' > 0;
									# Coefficiente di penetrazione dell'aria
			param fsa 'frontal surface area' > 0;
									# Area frontale del veicolo in mq
			param beta 'vehicle constant' := 0.5 * drc * fsa * ad;										# Parametro caratteristico dei veicoli
										
										
			### PARAMETRI RELATIVI ALLE CITTA ###

			param d{cities,cities} > 0; 	
									# Distanza tra due citta
			param angle{cities,cities};		
									# Pendenza media del tratto di strada in radianti
			param alpha{i in cities, j in cities} := 
				ga * (sin(angle[i,j]) + rrc * cos(angle[i,j]));
									# Parametri caratteristici degli archi


			### PARAMETRI DI SPEDIZIONE ###

			param twlb{cities} >= 0;# Time Window Lower Bound
			param twub{cities} >= 0;# Time Window Upper Bound
									# Finestre temporali (in ore)
			param st{cities} >= 0;	# Tempo di servizio dei clienti (in ore)
			param q{cities} >= 0;	# Domanda


			### PARAMETRI RELATIVI AGLI AUTISTI ###

			param driver_wage > 0; 	# Salario orario dell'autista (in euro/h)


			### PARAMETRI RELATIVI ALLE VELOCITA ###

			param slb 'speed lower bound' >= 0;
			param sub 'speed upper bound' >= 0;
									# Limiti globali di velocita (in metri/ora)
			param si 'speed intervals number' := card(speed_levels);
									# Numero di intervalli di suddivisione delle 
									# velocita
			param siw 'delta speed' := (sub - slb)/si;
									# Ampiezza dell'intervallo di velocita
			param speeds{r in speed_levels} := slb + (siw*(r-1) + siw*r)/2;
									# Velocita medie assumibili
		\end{amplcode}

		Segue la definizione delle variabili decisionali del modello e della funzione obiettivo.

		\begin{amplcode}
			var X{cities, cities} logical;
			var F{cities, cities} >= 0;
			var Z{speed_levels, cities, cities} logical;
			var Tourtime{j in clients} >= 0;	
									# Tempo speso nel seguire l’intera rotta che visita 
									# per ultima la citta j prima di tornare al
			# deposito
			var Arrivaltime{j in cities} >= 0;
									# Tempo di arrivo del veicolo al cliente j
												
			### OBJECTIVE ###

			minimize cost:
				(fc + ghgfe * ec) *	# Costo per ogni litro di carburante
									# (acquisto + emissioni) 
				1/(diesel_energy * engine_efficiency) *
									# Fattore di conversione da energia
									# a litri di carburante necessari
				(					# Energia totale necessaria
					sum {(i,j) in path} (
						alpha[i,j] * d[i,j] * uvw * X[i,j]
									# Energia necessaria al movimento del
									# veicolo senza carico
					) +
					sum {(i,j) in path} (
						alpha[i,j] * d[i,j] * F[i,j]
									# Energia necessaria al movimento del
									# carico (senza considerare il peso 
									# del veicolo)
					) + 
					sum {(i,j) in path} (
						d[i,j] * beta * (
							sum {r in speed_levels} (
								((speeds[r]/3600)^2) * Z[r,i,j] 
							)
						)			# Energia dissipata dalla resistenza 
									# dell'aria
					)
				) + 
				sum {c in clients} (driver_wage * Tourtime[c]);
		\end{amplcode}

		In ultimo vi è l’elencazione di tutti i vincoli a cui sono sottoposte le variabili decisionali. È interessante notare che ad ognuno di essi vi è associato un identificatore a scelta dell’utente. Ciò arricchisce di molto l’espressività del linguaggio e risulta particolarmente utile in fase di debugging.

		\begin{amplcode}
			subject to fleet_usage:
				sum {c in clients,dep in depot} X[dep,c] = fleet_dim;
								# Tutti i veicoli della flotta devono essere usati
								# per le consegne

			subject to single_exit {i in clients}:
				sum {j in cities} (X[i,j]) = 1; 
				
			subject to single_entrance {j in clients}:
				sum {i in cities} (X[i,j]) = 1;
								# Tutti i nodi devono essere visitati una ed una
								# ed una sola volta

			subject to flux_balance {i in clients}:
				sum {j in cities} (F[j,i] - F[i,j]) = q[i];
								# La differenza tra il flusso di carico entrante in 
								# un nodo ed il flusso uscente deve essere pari 
								# alla richiesta del nodo stesso

			subject to capacity_lower_constraint {(i,j) in path}:
				F[i,j] >= q[j] * X[i,j];
			subject to capacity_upper_constraint {(i,j) in path}:
				F[i,j] <= (max_load - q[i]) * X[i,j];
								# Il carico non deve superare la capacita massima
								# del veicolo

			subject to temporal_consistency {i in cities, j in clients: i <> j}:
				Arrivaltime[i] - Arrivaltime[j] + st[i] +
				sum {r in speed_levels} (
					(d[i,j] * Z[r,i,j]) / speeds[r]	
				) <= (1 - X[i,j]) * max(0, twub[i] + st[i] + (d[i,j] / slb ) - twlb[j]);
								# Vincolo di consistenza temporale
									
			subject to time_window {i in clients}:
				twlb[i] <= Arrivaltime[i] <= twub[i];
								# Time window

			subject to total_temporal_length {j in clients}:
				Arrivaltime[j] + st[j] - Tourtime[j] + 
				sum {r in speed_levels, dep in depot} (
					d[j,dep] * Z[r,j,dep] / speeds[r]
				) <= L * (1 - sum {dep in depot} X[j,dep]);
								# Vincolo sul tempo totale impiegato nei percorsi

			subject to speed_level_constraint {(i,j) in path}:
				sum {r in speed_levels} (Z[r,i,j]) = X[i,j];
								# Vincolo sulla velocita assumibile
		\end{amplcode}

% section implementazione (end)